apply plugin: 'com.android.library'
apply plugin: 'maven'

def repositoryPropertiesFile = rootProject.file('repository.properties')
def repositoryProperties = new Properties()
repositoryProperties.load(new FileInputStream(repositoryPropertiesFile))

def _VersionCode = 2
def _VersionName = '0.0.2'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode _VersionCode as int
        versionName _VersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false;
    }

    defaultPublishConfig "release"
}

configurations {
    deployerJars
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: repositoryProperties['MAVEN_URL']) {
                authentication (
                    userName: repositoryProperties['MAVEN_USERNAME'],
                    password: repositoryProperties['MAVEN_PASSWORD']
                )
            }

            pom.version = _VersionName
            pom.groupId = 'com.eju.cy'
            pom.artifactId = 'router'
        }
    }
}

task releaseJar(type: Jar) {
    baseName 'libEjuRouter'
    classifier 'release'

    FileTree tree = fileTree('build/intermediates/classes/release')
    tree.exclude('**/BuildConfig.class', '**/R.class')
    tree.exclude('**/R$*.class')
    from tree

    destinationDir file('../library/')
}
releaseJar.dependsOn(build)

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    provided 'com.android.support:appcompat-v7:24.2.0'
    testCompile 'junit:junit:4.12'

    // http
    provided 'com.mcxiaoke.volley:library:1.0.19'
    provided 'com.squareup.okhttp3:okhttp:3.3.1'

    // json
    provided 'com.google.code.gson:gson:2.7'

    //  Robolectric
    testCompile "org.robolectric:robolectric:3.1.2"

    //  AssertJ
    testCompile 'com.squareup.assertj:assertj-android:1.1.0'

    //  PowerMock + Mockito
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile "org.powermock:powermock-module-junit4-rule:1.6.5"
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile "org.powermock:powermock-classloading-xstream:1.6.5"

    // maven deployer
    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}
